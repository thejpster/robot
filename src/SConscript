"""
Wrapper around the main Scons file (./src/SConscript) which puts all the
intermediates in ./bin

Copyright (c) 2014 theJPster (pwrs@hejpster.org.uk)

PWRS is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

PWRS is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with PWRS.  If not, see <http://www.gnu.org/licenses/>.
"""

env = Environment(
    CPPFLAGS = [
        "-g",
        "-O1",
        "-std=c99",
        "-Wall",
        "-pedantic"
        ],
    CPPPATH = [
        "."
    ],
    CPPDEFINES = {
        "_GNU_SOURCE" : 1,
    }
    )

if ARGUMENTS.get('USE_WIRINGPI') != '0':
    env.Append(CPPDEFINES={'USE_WIRINGPI': 1})
    env.Append(LIBS=['wiringPi'])

if ARGUMENTS.get('LCD_SIM') == '1':
    env.Append(CPPDEFINES={'LCD_SIM': 1})

if ARGUMENTS.get('VERBOSE') != '1':
    env['CCCOMSTR'] = 'Compiling $TARGET'
    env['LINKCOMSTR'] = 'Linking $TARGET'

files = env.Glob('*/src/*.c') + [ 'robot.c', 'modes.c' ]

env.Program('pwrs', files)
